plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.jmh"
    id "ru.vyarus.animalsniffer"
}

description = 'OpenTelemetry SDK'
ext.moduleName = "io.opentelemetry.sdk"
ext.propertiesDir = "build/generated/properties/io/opentelemetry/sdk"
archivesBaseName = "opentelemetry-sdk"

dependencies {
    api project(':api:all'),
            project(':sdk:common'),
            project(':sdk:trace')

    annotationProcessor "com.google.auto.value:auto-value"

    testAnnotationProcessor "com.google.auto.value:auto-value"

    testImplementation project(':sdk:testing')
    testImplementation "org.junit-pioneer:junit-pioneer"
}

sourceSets {
    main {
        output.dir("build/generated/properties", builtBy: 'generateVersionResource')
    }
}

animalsniffer {
    // Don't check sourceSets.jmh and sourceSets.test
    sourceSets = [
            sourceSets.main
    ]
}

task generateVersionResource {
    doLast {
        def folder = file(propertiesDir)
        folder.mkdirs()
        def propertiesFile = new File(folder.getAbsolutePath(), "version.properties")
        propertiesFile.write("sdk.version=${project.version}")
    }
}
