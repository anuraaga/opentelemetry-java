plugins {
    id "java"
    id "maven-publish"

    id "com.github.johnrengelman.shadow"
    id "me.champeau.gradle.jmh"
    id "org.unbroken-dome.test-sets"
    id "ru.vyarus.animalsniffer"
}

description = "OpenTelemetry Context (Incubator)"
ext.moduleName = "io.opentelemetry.context"

testSets {
    grpcInOtelTest
    otelInGrpcTest

    braveInOtelTest
    otelInBraveTest
    otelAsBraveTest

    storageWrappersTest
}

dependencies {
    implementation "com.blogspot.mydailyjava:weak-lock-free:0.18"

    grpcInOtelTestImplementation libraries.grpc_context
    otelInGrpcTestImplementation libraries.grpc_context

    braveInOtelTestImplementation "io.zipkin.brave:brave:5.13.1"
    otelAsBraveTestImplementation "io.zipkin.brave:brave:5.13.1"
    otelInBraveTestImplementation "io.zipkin.brave:brave:5.13.1"

    testImplementation libraries.awaitility
    testImplementation libraries.guava
    testImplementation libraries.junit_pioneer

    signature libraries.android_signature
}

shadowJar {
    archiveClassifier = ""
    relocate "com.blogspot.mydailyjava.weaklockfree", "io.opentelemetry.internal.shaded"
}

tasks {
    jar {
        enabled = false
        dependsOn("shadowJar")
    }
}

publishing {
    publications {
        named("mavenPublication") {
            artifact(tasks.shadowJar.archiveFile)
        }
    }
}
