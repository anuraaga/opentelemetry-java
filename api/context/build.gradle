plugins {
    id "java-library"
    id "maven-publish"

    id "me.champeau.gradle.jmh"
    id "org.unbroken-dome.test-sets"
    id "ru.vyarus.animalsniffer"
}

description = "OpenTelemetry Context (Incubator)"
ext.moduleName = "io.opentelemetry.context"
archivesBaseName = "opentelemetry-context"

testSets {
    grpcInOtelTest
    otelInGrpcTest

    braveInOtelTest
    otelInBraveTest
    otelAsBraveTest

    storageWrappersTest

    strictContextEnabledTest
}

dependencies {
    grpcInOtelTestImplementation "io.grpc:grpc-context"
    otelInGrpcTestImplementation "io.grpc:grpc-context"

    braveInOtelTestImplementation "io.zipkin.brave:brave"
    otelAsBraveTestImplementation "io.zipkin.brave:brave"
    otelInBraveTestImplementation "io.zipkin.brave:brave"

    testImplementation "org.awaitility:awaitility"
    testImplementation "com.google.guava:guava"
    testImplementation "org.junit-pioneer:junit-pioneer"
}

tasks.named("strictContextEnabledTest").configure {
    jvmArgs "-Dio.opentelemetry.context.enableStrictContext=true"
}

check.dependsOn(
        grpcInOtelTest,
        otelInGrpcTest,
        braveInOtelTest,
        otelInBraveTest,
        otelAsBraveTest,
        storageWrappersTest,
        strictContextEnabledTest
)
